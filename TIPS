** browse-url

   By following setting, "C-x m" on URL like string calls emacs-w3m. And
   you can preview HTML file by "C-c C-v" on html-mode which is
   distributed with Emacs21.

(setq browse-url-browser-function 'w3m-browse-url)
(autoload 'w3m-browse-url "w3m" "Ask a WWW browser to show a URL." t)
(global-set-key "\C-xm" 'browse-url-at-point)

   If you want to use the other browser than emacs-w3m when "C-x m" is
   typed in w3m-mode buffers, you can put the following setting to your
   ~/.emacs.

(defadvice browse-url-at-point
  (around change-browse-url-browser-function activate compile)
  (let ((browse-url-browser-function
         (if (eq major-mode 'w3m-mode)
             'browse-url-netscape
           'w3m-browse-url)))
    ad-do-it))



** dired

   By "C-x m" on a html file in dired-mode, you can open it by
   emacs-w3m.

(add-hook 'dired-mode-hook
          (lambda ()
              (define-key dired-mode-map "\C-xm" 'dired-w3m-find-file)))
(defun dired-w3m-find-file ()
  (interactive)
  (require 'w3m)
  (let ((file (dired-get-filename)))
    (if (y-or-n-p (format "Open 'w3m' %s " (file-name-nondirectory file)))
        (w3m-find-file file))))



** hnf-mode

   With the following setting, you can see the newest diary via
   emacs-w3m by "C-c C-b" in hnf-mode.

(autoload 'w3m-browse-url "w3m" nil t)
(defun w3m-hnf-browse-url-w3m (url &optional new-window)
  (interactive (browse-url-interactive-arg "URL: "))
  (save-selected-window
    (pop-to-buffer (get-buffer-create "*w3m*"))
    (w3m-browse-url url new-window)))
(setq hnf-browse-url-browser-function (function w3m-hnf-browse-url-w3m))



** Gnus

   Now nnshimbun.el (which was contained in T-gnus in the past) is a
   module of the shimbun library.  You can read web newspapers or web
   mailing list archives, etc. so conveniently by using nnshimbun.el and
   Gnus (5.10, 5.9) or T-gnus.  Put the following line in your
   ~/.gnus.el file if you are using Gnus (T-gnus users don't need to do
   it):

(autoload 'gnus-group-make-shimbun-group "nnshimbun" nil t)

   You can read various contents supported by the shimbun library as if
   you were reading mail or news articles by invoking this command (M-x
   gnus-group-make-shimbun-group).

   In addition, if you are using Gnus 5.10 (or Oort Gnus versions over
   v0.06), you only have to add the following line in your ~/.gnus.el
   file to make Gnus show text/html contents using emacs-w3m instead of
   Emacs/w3.

(setq mm-text-html-renderer 'w3m)

   By default, Gnus will not apply the treatment variables, for instance
   `gnus-treat-strip-banner', to text/html parts.  To have them applied
   to text/html parts automatically, there are two ways to do that:

;; Apply all the treatments to text/html parts.
(eval-after-load "gnus-art"
  '(add-to-list 'gnus-article-treat-types "text/html"))

;; Apply a certain treatment to text/html parts.
(setq gnus-treat-strip-banner '(or t (typep "text/html")))

   See info ``Customizing Articles'' for details.

   In additon, the experimental code below is used to display
   multipart/related pictures. The place might be something wrong :-<

;; Date: Fri, 27 Jul 2001 12:51:12 +0900
;; Message-ID: <yosuwv4v3u8f.fsf@jpl.org>
;; From: Katsumi Yamaoka <yamaoka@jpl.org>
;; To: semi-gnus-ja@meadowy.org
;; Subject: [nnshimbun] toggle inline images

(eval-after-load "gnus-art"
  '(or (assoc "multipart/related" gnus-mime-multipart-functions)
       (setq gnus-mime-multipart-functions
	     (cons
	      (cons
	       "multipart/related"
	       (byte-compile
		(lambda (handle)
		  (gnus-mime-display-mixed (cdr handle)))))
	      gnus-mime-multipart-functions))))



** yahtml-mode

   You can preview the html file with emacs-w3m, which you are editing
   in yahtml-mode.

(autoload 'w3m-goto-url "w3m")
(defadvice yahtml-browse-html
  (around w3m-yahtml-browse-html activate compile)
  (w3m-goto-url (ad-get-arg 0) t))


Local Variables:
mode: indented-text
fill-column: 72
End:
