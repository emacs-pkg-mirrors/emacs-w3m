■■ browse-url

   以下のように設定しておくと，URI に類似した文字列がある場所で C-x m と
   入力すれば，emacs-w3m で表示されるようになります．また Emacs21 などに
   附属するhtml-mode で C-c C-v とすれば編集の HTML ファイルを emacs-w3m
   で preview できます．

(setq browse-url-browser-function 'w3m-browse-url)
(autoload 'w3m-browse-url "w3m" "Ask a WWW browser to show a URL." t)
(global-set-key "\C-xm" 'browse-url-at-point)

   もしも，w3m-mode のバッファ内部で C-x m と打鍵した場合には，emacs-w3m
   を利用せずに，直接，外部ブラウザを呼び出したい場合には，以下のように
   設定してください．

(defadvice browse-url-at-point
  (around change-browse-url-browser-function activate compile)
  (let ((browse-url-browser-function
         (if (eq major-mode 'w3m-mode)
             'browse-url-netscape
           'w3m-browse-url)))
    ad-do-it))



■■ dired

   以下のように設定しておくと，dired-mode のバッファでファイルを選択して
   いる状態で C-x m と入力すれば，該当ファイルが emacs-w3m で表示される
   ようになります．

(add-hook 'dired-mode-hook
          (lambda ()
            (define-key dired-mode-map "\C-xm" 'dired-w3m-find-file)))
(defun dired-w3m-find-file ()
  (interactive)
  (require 'w3m)
  (let ((file (dired-get-filename)))
    (if (y-or-n-p (format "Open 'w3m' %s " (file-name-nondirectory file)))
        (w3m-find-file file))))



■■ hnf-mode

   以下のように設定しておくと，hnf-mode にて C-c C-b と入力すれば，
   emacs-w3m で最新の日記が表示されるようになります．

(autoload 'w3m-browse-url "w3m" nil t)
(defun w3m-hnf-browse-url-w3m (url &optional new-window)
  (interactive (browse-url-interactive-arg "URL: "))
  (save-selected-window
    (pop-to-buffer (get-buffer-create "*w3m*"))
    (w3m-browse-url url new-window)))
(setq hnf-browse-url-browser-function (function w3m-hnf-browse-url-w3m))



■■ Gnus

   過去に T-gnus などに含まれていた nnshimbun.el は，shimbun ライブラリ
   の一モジュールになりました．これを Gnus (5.10, 5.9) や T-gnus などと
   ともに使うことによって，web で公開されている新聞記事やメーリングリス
   トのアーカイブを，非常に便利に読むことができます．Gnus では以下の行を
   ~/.gnus.el ファイルに入れて下さい (T-gnus では不要):

(autoload 'gnus-group-make-shimbun-group "nnshimbun" nil t)

   このコマンド (M-x gnus-group-make-shimbun-group) を group バッファで
   唱えることによって，shimbun ライブラリがサポートしている各種のコンテ
   ンツを，あたかもメールやニュース記事のように読むことができます．

   加えて，Gnus 5.10 (または v0.06 以上の Oort Gnus) を使っている場合は，
   以下の行を .gnus ファイルに追加するだけで text/html のコンテンツの表
   示に Emacs/w3 の代わりに emacs-w3m を使うようにすることができます．

(setq mm-text-html-renderer 'w3m)

   デフォルトでは，Gnus は例えば `gnus-treat-strip-banner' のような
   treatment 変数を text/html パートに適用しません．自動的に行なわせるに
   は二つの方法があります:

;; すべての treatment を text/html パートに適用する．
(eval-after-load "gnus-art"
  '(add-to-list 'gnus-article-treat-types "text/html"))

;; 特定の treatment を text/html パートに適用する．
(setq gnus-treat-strip-banner '(or t (typep "text/html")))

   詳しいことは info の ``Customizing Articles'' を見て下さい．

   加えて，以下の実験的なコードは multipart/related な絵を見るためのもの
   です．表示される場所に難があります．:-<

;; Date: Fri, 27 Jul 2001 12:51:12 +0900
;; Message-ID: <yosuwv4v3u8f.fsf@jpl.org>
;; From: Katsumi Yamaoka <yamaoka@jpl.org>
;; To: semi-gnus-ja@meadowy.org
;; Subject: [nnshimbun] toggle inline images

(eval-after-load "gnus-art"
  '(or (assoc "multipart/related" gnus-mime-multipart-functions)
       (setq gnus-mime-multipart-functions
	     (cons
	      (cons
	       "multipart/related"
	       (byte-compile
		(lambda (handle)
		  (gnus-mime-display-mixed (cdr handle)))))
	      gnus-mime-multipart-functions))))



■■ yahtml-mode

   以下のように設定しておくと，yahtml-mode で編集中のファイルを preview
   する時に emacs-w3m を利用することができます．

(autoload 'w3m-goto-url "w3m")
(defadvice yahtml-browse-html
  (around w3m-yahtml-browse-html activate compile)
  (w3m-goto-url (ad-get-arg 0) t))


Local Variables:
mode: indented-text
coding: euc-japan-unix
use-kuten-for-period: nil
use-touten-for-comma: nil
fill-column: 72
End:
